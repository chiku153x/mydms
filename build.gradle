plugins {
    id "de.abas.esdk" version "1.2.6"
}

repositories {
    exclusiveContent {
        forRepository {
            maven {
                url "http://$NEXUS_HOST:$NEXUS_PORT/nexus/content/repositories/$NEXUS_NAME-SNAPSHOT"
                allowInsecureProtocol = true
            }
        }
        filter {
            // restrict this repository to lookup clientdir libs only
            includeGroup('de.abas.clientdir')
        }
    }
    exclusiveContent {
        forRepository {
            maven {
                url "http://$NEXUS_HOST:$NEXUS_PORT/nexus/content/repositories/$NEXUS_NAME"
                allowInsecureProtocol = true
            }
        }
        filter {
            // restrict this repository to lookup homedir libs only
            includeGroup('de.abas.homedir')
        }
    }
    // abas.maven-pubic repository for additional ESDK libraries
    maven {
        url 'https://artifactory.abas.sh/artifactory/abas.maven-public/'
        mavenContent {
            releasesOnly()
        }
        content {
            includeGroupByRegex('de\\.abas\\..*')
        }
    }
//    // Include if using external libraries from public repository
//    mavenCentral()
}

// Create flag to simplify ERP version specific configuration settings below.
// Useful to select different AJO libraries or 'esdk.app.essentialsVersions'.
// 'targetErpVersion' can be provided in 'gradle.properties' or as command line parameter.
// Default is a version below '2200' (i.e. before ERP 22).
Boolean forErp22 = targetErpVersion.majorVersion >= 2200

esdk {
    app {
        name = "mydocmgt"
        vendorId = "xx"
        appId = "mydms"
        shared = false
        infosystems = ["IS.OW1.DOCHANDLER"]
        tables = ["Firma","Verkauf","Kunde","Lieferant","Teil:Artikel","Betr-Auftrag","Einkauf","Chargen","DocumentManagement"]
        screens = [:]
        advancedScreens=["50":["D"], "32":["A"], "0":["D"],"1":["D"],"2":["D"],"77":["A"],"35":["A"],"36":["A"],"42":["A"],"45":["A"],"46":["A"],"143":["A"],"70":["A"],"451":["A"]]
        data = []
        enums = ["DocumentManagementScreens"]
        namedTypes = []
        languages = "DA"
        essentialsVersions = [ "2017r1n00-2017r4n16", "2018r1n00-2018r4n16", "2019r1n00-2019r4n16" ]
    }

    abas {
        homeDir = ABAS_HOMEDIR
        clientDir = ABAS_CLIENTDIR
        clientId = ABAS_CLIENTID
        edpHost = EDP_HOST
        edpPort = EDP_PORT.toInteger()
        edpUser = EDP_USER
        edpPassword = EDP_PASSWORD
    }

    nexus {
        nexusHost = NEXUS_HOST
        nexusPort = NEXUS_PORT.toInteger()
        nexusRepoName = NEXUS_NAME
        nexusUserName = NEXUS_USER_NAME
        nexusPassword = NEXUS_PASSWORD
        nexusVersion = NEXUS_VERSION
    }

    ssh {
        host = SSH_HOST
        port = SSH_PORT.toInteger()
        user = SSH_USER
        password = SSH_PASSWORD
        key = SSH_KEY
    }
    installType = "SSH"
}

group = 'com.chiku.apps'

// Always generate code for Java 8
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

dependencies {
    constraints {
        implementation("org.apache.logging.log4j:log4j-core") {
            version {
                strictly("[2.17, 3[")
                prefer("2.17.1")
            }
            because("CVE-2021-44228, CVE-2021-45046, CVE-2021-45105: " +
                    "Log4j vulnerable to remote code execution and other critical security vulnerabilities")
        }
    }

    if (forErp22) {
        // Some libraries have been renamed in ERP 22 - here we use the new names

        // AJO interfaces
        provided 'de.abas.homedir:abas-ajo-db:1.0.0'
        // fixed standard enumerations
        provided 'de.abas.homedir:abas-ajo-common-type-enums-base:1.0.0'
        // generated standard enumerations
        provided 'de.abas.homedir:abas-ajo-common-type-enums-standard:1.0.0'
        // base types, super class for enumerations
        provided 'de.abas.homedir:abas-ajo-common:1.0.0'
        // JFOP-Server API
        provided 'de.abas.homedir:abas-jfop-rt-api:1.0.0'
        // Classes to execute FO commands
        //provided 'de.abas.homedir:abas-jfop-api:1.0.0'

        // AJO-AXI2 classes
        implementation 'de.abas.homedir:abas-ajo-axi2:1.0.0'
        // AJO-AXI classes
        implementation 'de.abas.homedir:abas-ajo-axi:1.0.0'
        // AJO interface implementation
        implementation 'de.abas.homedir:abas-ajo-db-internal:1.0.0'
        // Helper classes for AJO framework an generator
        //implementation 'de.abas.homedir:abas-ajo-db-common:1.0.0'
        // Helper classes to handle selections, fields, meta data, buffers, and contexts
        implementation 'de.abas.homedir:abas-ajo-db-util:1.0.0'
    } else {
        // AJO interfaces
        provided 'de.abas.homedir:abas-db-base:1.0.0'
        // standard enumerations
        provided 'de.abas.homedir:abas-enums:1.0.0'
        // base types, super class for enumerations
        provided 'de.abas.homedir:abas-erp-common:1.0.0'
        // JFOP-Server API
        provided 'de.abas.homedir:abas-jfop-runtime-api:1.0.0'
        // Classes to execute FO commands
        //provided 'de.abas.homedir:abas-jfopapi:1.0.0'

        // AJO-AXI2 classes
        implementation 'de.abas.homedir:abas-axi2:1.0.0'
        // AJO-AXI classes
        implementation 'de.abas.homedir:abas-axi:1.0.0'
        // AJO interface implementation
        implementation 'de.abas.homedir:abas-db-internal:1.0.0'
        // Helper classes for AJO framework an generator
        //implementation 'de.abas.homedir:abas-db-common:1.0.0'
        // Helper classes to handle selections, fields, meta data, buffers, and contexts
        implementation 'de.abas.homedir:abas-db-util:1.0.0'
    }

    // Java library for EDP access
    provided 'de.abas.homedir:jedp:1.0.0'

    // Generated AJO classes: databases, groups, custom enumerations, units
    implementation 'de.abas.clientdir:abas-db:1.0.0-SNAPSHOT'
    // Generated indexes. Used to determine the AJO editor class from db- and group-number.
    implementation 'de.abas.clientdir:abas-db-index:1.0.0-SNAPSHOT'
    // Generated AJO classes: infosystems
    implementation 'de.abas.clientdir:abas-db-infosys:1.0.0-SNAPSHOT'
    // Helper classes, e.g. InputBox, GUISelectionBuilder, TextBox, MenuBuilder
    //implementation 'de.abas.homedir:abas-erpapi:1.0.0'

    // Base classes to access ERP via JFOP context.
    // Among other things, the buffer classes are stored here (e.g. GlobalTextBuffer, ScreenBuffer, UserTextBuffer).
    runtimeOnly 'de.abas.homedir:abas-jfop-base:1.0.0'
    runtimeOnly 'de.abas.homedir:commons-collections:1.0.0'
    // Supports jcl logging by mapping it to slf4j
    runtimeOnly 'de.abas.homedir:jcl-over-slf4j:1.0.0'
    // Logging API
    runtimeOnly 'de.abas.homedir:slf4j-api:1.0.0'

    // Library supporting unit and integration tests
    testImplementation 'junit:junit:4.13.2'
    // Assertion matcher for JUnit
    testImplementation 'org.hamcrest:hamcrest-all:1.3'

    // Helper classes for AJO integration tests in ESDK
    integTestImplementation('de.abas.esdk.test.util:esdk-test-utils:0.0.9') {
        if (forErp22) {
            capabilities {
                requireCapability('de.abas.esdk.test.util:esdk-test-utils-v22-support')
            }
        } else {
            capabilities {
                requireCapability('de.abas.esdk.test.util:esdk-test-utils-pre22-support')
            }
        }
    }
}
